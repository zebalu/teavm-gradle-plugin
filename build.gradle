apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'dev.gradleplugins.java-gradle-plugin'
apply plugin: 'java-gradle-plugin'

java.sourceCompatibility = 'VERSION_1_8'
java.targetCompatibility = 'VERSION_1_8'

buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.12.0'
        classpath 'dev.gradleplugins:gradle-plugin-development:1.1'
    }
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

repositories { jcenter() }

dependencies {

    implementation group: 'org.teavm', name: 'teavm-tooling', version: teavmVersion
    implementation group: 'org.teavm', name: 'teavm-cli', version: teavmVersion

    implementation gradleApi()
    implementation localGroovy()

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.1.0'

    testImplementation gradleTestKit()

    // https://mvnrepository.com/artifact/org.junit.vintage/junit-vintage-engine
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.5.2'


}

test {
    useJUnitPlatform()
}

gradlePlugin {
    pluginSourceSet project.sourceSets.main
    testSourceSets project.sourceSets.test
    plugins {
        teavmPlugin {
            id = 'io.github.zebalu.teavm-gradle-plugin'
            implementationClass = 'io.github.zebalu.gradle.teavm.TeavmGradlePlugin'
        }
    }
}

task functionalTestClasses() {

}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
    from 'src/main/resources'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    dependsOn javadoc
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "zebalu"
            name "Bal√°zs Zaicsek"
            email "zebalu@gmail.com"
        }
    }

    scm { url "https://github.com/zebalu/teavm-gradle-plugin" }
}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Extend gradle with the capability of compiling bytecdoe to JavasCript.')
                root.appendNode('name', 'Teavm Gradle Plugin')
                root.appendNode('url', 'https://github.com/zebalu/teavm-gradle-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = getPropertyOrWarn('bintray.user')
    key = getPropertyOrWarn('bintray.key')
    publications = ['maven']

    pkg {
        repo = 'releases'
        name = 'teavm-gradle-plugin'
        userOrg = 'zebalu'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/zebalu/gradle-aspectj'
    }
}

pluginBundle {
    website = 'https://github.com/zebalu/teavm-gradle-plugin'
    vcsUrl = 'https://github.com/zebalu/teavm-gradle-plugin'

    description = 'TeaVM Gradle Plugin is bridge to use TeaVM with gradle and compile Java to JavaScript.'

    plugins {
        teavmPlugin {
            displayName = 'Gradle Greeting plugin'
            tags = ['teavm', 'bytecode', 'javascript', 'transpile']
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}


String getPropertyOrWarn(String propertyName) {
    if (!project.hasProperty(propertyName)) {
        logger.warn("There is no property set with name: ${propertyName}. Publication might fail.")
        return 'missing'
    }
    return project.getProperty(propertyName)
}
